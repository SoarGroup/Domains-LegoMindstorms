sp {flipflop*propose*init-agent
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator <o> +)
   (<o> ^name init-agent)
}

sp {flipflop*apply*init-agent
   (state <s> ^operator.name init-agent)
-->
   (<s> ^name flipflop
        ^direction none)
}

sp {flipflop*apply*any-op
   (state <s> ^name flipflop
              ^io.output-link <out>)
   (<out> ^<cmd> <val>)
   (<val> ^status <status>)
-->
   (<out> ^<cmd> <val> -)
}

sp {flipflop*propose*wait
   (state <s> ^name flipflop
             -^operator.name wait)
-->
   (<s> ^operator <o> + <)
   (<o> ^name wait)
}

sp {flipflop*propose*start-robot
   (state <s> ^name flipflop
              ^io.input-link.brick.buttons.button <enter>
              ^direction none)
   (<enter> ^name enter
            ^prev-state up
            ^cur-state down)
-->
   (<s> ^operator <o> +)
   (<o> ^name start-robot)
}

sp {flipflop*apply*start-robot
   (state <s> ^name flipflop
              ^operator.name start-robot
              ^io.output-link <out>
              ^direction none)
-->
   (<s> ^direction none -
        ^direction forward)
   (<out> ^motor <m1>
          ^motor <m2>)
   (<m1> ^port 1
         ^start <s1>)
   (<m2> ^port 2
         ^start <s2>)
   (<s1> ^direction forward
         ^power 20)
   (<s2> ^direction forward
         ^power 20)
}

sp {flipflop*propose*stop-robot
   (state <s> ^name flipflop
              ^io.input-link.brick.buttons.button <enter>
              ^direction << forward backward >>)
   (<enter> ^name enter
            ^prev-state up
            ^cur-state down)
-->
   (<s> ^operator <o> +)
   (<o> ^name stop-robot)
}

sp {flipflop*apply*stop-robot
   (state <s> ^name flipflop
              ^operator.name stop-robot
              ^io.output-link <out>
              ^direction { <dir> << forward backward >> })
-->
   (<s> ^direction <dir> -
        ^direction none)
   (<out> ^motor <m1>
          ^motor <m2>)
   (<m1> ^port 1
         ^stop <s1>)
   (<m2> ^port 2
         ^stop <s2>)
}

sp {flipflop*propose*flip-dir
   (state <s> ^name flipflop
              ^io.input-link <in>
              ^direction <dir>)
   (<in> ^sensor <touch>)
   (<touch> ^type touch
            ^pressed true 
            ^prev-pressed false)
-->
   (<s> ^operator <o> +)
   (<o> ^name flip-dir
        ^direction <dir>)
}

sp {flipflop*apply*flip-dir*forward
   (state <s> ^name flipflop
              ^operator <o>
              ^direction backward
              ^io.output-link <out>)
   (<o> ^name flip-dir
        ^direction backward)
-->
   (<s> ^direction backward -
        ^direction forward)
   (<out> ^motor <m1>
          ^motor <m2>)
   (<m1> ^port 1
         ^set.direction forward)
   (<m2> ^port 2
         ^set.direction forward)
} 

sp {flipflop*apply*flip-dir*backward
   (state <s> ^name flipflop
              ^operator <o>
              ^direction forward
              ^io.output-link <out>)
   (<o> ^name flip-dir
        ^direction forward)
-->
   (<s> ^direction forward -
        ^direction backward)
   (<out> ^motor <m1>
          ^motor <m2>)
   (<m1> ^port 1
         ^set.direction backward)
   (<m2> ^port 2
         ^set.direction backward)
} 