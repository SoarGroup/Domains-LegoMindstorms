
sp {traverse-junction-real-world*propose*align-to-all-black
	(state <s>	^name traverse-junction-real-world
				^superstate.needs-all-black-alignment)
-->
	(<s>	^operator.name align-to-all-black)
}

sp {traverse-junction-real-world*apply*align-to-all-black*finish
	(state <s>	^operator.name align-to-all-black
				^color-sensor-values <csv>
				^io.output-link <out>
				^left-motor-port <left-port>
				^right-motor-port <right-port>
				^superstate <ss>)
	(<ss>	^operator.name traverse-junction-real-world
			^needs-all-black-alignment <needs>)
	(<csv>	^left black
			^right black
			^center black)
-->
	(<ss>	^needs-all-black-alignment <needs> -)
	(<out>	^motor <left>
			^motor <right>)
	(<left>		^port <left-port>
				^set-direction forward
				^set-power 0)
	(<right>	^port <right-port>
				^set-direction forward
				^set-power 0)
}

## Alignment contingencies

sp {traverse-junction-real-world*apply*align-to-all-black*contingency*left-backward
	(state <s>	^operator.name align-to-all-black
				^color-sensor-values <csv>
				^io.output-link <out>
				^left-motor-port <left-port>
				^right-motor-port <right-port>
				^superstate <ss>
				^relative-direction << left backward >>)
	(<ss>	^junction-color <color>
			^operator.name traverse-junction-real-world)
	(<csv>	^<< left right >> <color>
			^center black)
-->
	(<out>	^motor <left>
			^motor <right>)
	(<left>		^port <left-port>
				^set-direction forward
				^set-power 20)
	(<right>	^port <right-port>
				^set-direction backward
				^set-power 20)
}

sp {traverse-junction-real-world*apply*align-to-all-black*contingency*right
	(state <s>	^operator.name align-to-all-black
				^color-sensor-values <csv>
				^io.output-link <out>
				^left-motor-port <left-port>
				^right-motor-port <right-port>
				^superstate <ss>
				^relative-direction right)
	(<ss>	^junction-color <color>
			^operator.name traverse-junction-real-world)
	(<csv>	^<< left right >> <color>
			^center black)
-->
	(<out>	^motor <left>
			^motor <right>)
	(<left>		^port <left-port>
				^set-direction backward
				^set-power 20)
	(<right>	^port <right-port>
				^set-direction forward
				^set-power 20)
}

