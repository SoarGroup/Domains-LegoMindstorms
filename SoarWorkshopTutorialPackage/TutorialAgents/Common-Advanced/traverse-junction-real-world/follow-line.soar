# Proposals for follow-line within the traverse-junction-real-world substate


# First the robot follows the line until the front sensors are off the junction (both white)
# This will center the motors over the junction
sp {traverse-junction-real-world*propose*follow-line
  (state <s> ^name traverse-junction-real-world
              ^top-state <top>)
  (<top> -^centered-on-junction)
-->
  (<s> ^operator <o> +)
  (<o> ^name follow-line)
}

sp {traverse-junction-real-world*apply*follow-line*mark*centered-on-junction
  (state <s> ^name traverse-junction-real-world
              ^operator.name follow-line
              ^color-sensor-values <vals>
              ^top-state <top>)
  (<vals> ^left white
           ^right white)
  (<top> -^centered-on-junction)
-->
  (<top> ^centered-on-junction true)
}

# If after the turn the robot is still on the junction, 
#   It will follow the line forward until it is off (both white)
sp {traverse-junction-real-world*propose*follow-line*move-off-junction
  (state <s> ^name traverse-junction-real-world
              ^top-state <top>)
  (<top> ^turned true
         -^moved-off-junction)
-->
  (<s> ^operator <o> +)
  (<o> ^name follow-line)
}

sp {traverse-junction-real-world*apply*follow-line*move-off-junction*mark*moved-off
  (state <s> ^name traverse-junction-real-world
              ^operator.name follow-line
              ^color-sensor-values <colors>
              ^top-state <top>)
  (<colors> ^left white
             ^right white)
  (<top> ^turned true)
-->
  (<top> ^moved-off-junction true)
}

