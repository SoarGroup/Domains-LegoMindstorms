
sp {traverse-junction-real-world*propose*move-to-center
	(state <s>	^name traverse-junction-real-world
				^superstate <ss>)
	(<ss>		-^has-moved-to-center)
-->
	(<s>	^operator.name move-to-center)
}

sp {traverse-junction-real-world*apply*move-to-center*motor-commands
	(state <s>	^operator.name move-to-center
				^superstate <ss>
				^left-motor-port <left-port>
				^right-motor-port <right-port>)
	(<ss>	^io.output-link <out>
			-^motor-commands
			^operator.name traverse-junction-real-world)
-->
	(<ss>	^motor-commands true)
	(<out>	^motor <left>
			^motor <right>)
	(<left>		^port <left-port>
				^set-direction forward
				^set-power 10)
	(<right>	^port <right-port>
				^set-direction forward
				^set-power 10)
}

sp {traverse-junction-real-world*apply*move-to-center*has-moved-to-center
	(state <s>	^operator.name move-to-center
				^superstate <ss>)
	(<ss>	^operator.name traverse-junction-real-world
			^color-sensor-values <csv>
			^io.output-link <out>
			^left-motor-port <left-port>
			^right-motor-port <right-port>)
	(<csv>	^left black
			^right black
			^center black)
-->
	(<ss>	^has-moved-to-center true)
	(<out>	^motor <left>
			^motor <right>)
	(<left>		^port <left-port>
				^set-direction forward
				^set-power 0)
	(<right>	^port <right-port>
				^set-direction forward
				^set-power 0)
}
