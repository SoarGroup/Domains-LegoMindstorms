
sp {traverse-junction-real-world*propose*move-off-junction
	(state <s>	^name traverse-junction-real-world
				^superstate <ss>)
	(<ss>		-^needs-all-black-alignment
				^turned)
-->
	(<s>	^operator.name move-off-junction)
}

sp {traverse-junction-real-world*apply*move-off-junction
	(state <s>	^operator.name move-off-junction
				^superstate <ss>
				^io.output-link <out>
				^left-motor-port <left-port>
				^right-motor-port <right-port>)
	(<ss>	^operator.name traverse-junction-real-world)
-->
	(<out>	^motor <left>
			^motor <right>)
	(<left>		^port <left-port>
				^set-direction forward
				^set-power 10)
	(<right>	^port <right-port>
				^set-direction forward
				^set-power 10)
}

sp {traverse-junction-real-world*apply*move-off-junction*done
	(state <s>	^operator.name move-off-junction
				^color-sensor-values <csv>
				^superstate <ss>)
	(<csv>	^<< left right center >> <color>)
	(<ss>	^operator.name traverse-junction-real-world
			^junction-color <color>)
-->
	(<ss>	^moved-off-junction-almost true)
}
