
sp {propose*exit-junction
	(state <s>	^name line-follower
				^junction-color <color>
        ^moved-off-junction true)
-->
	(<s>	^operator <o> + >)
	(<o>	^name exit-junction
       ^action stop)
}

# Add to traverse-junction-real-world for fix to seeing red as brown
sp {line-follower*elaborate*exit-junction*items*to*remove
  (state <s> ^name line-follower
              ^operator <o> +)
  (<o> ^name << exit-junction reset-junction-color >>)
-->
  (<o>
			^item selected-direction
      ^item centered-on-junction
      ^item moved-off-junction
			^item aligned-to-junction
			^item has-centered
			^item mark
      ^item desired-rotation
			^item turned
			^item moved-off-junction-almost
			^item moved-off-junction
			^item has-moved-to-center
			^item junction-color
			^item seen-junction-color
			^item seen-black
			^item motor-commands
			^item failed
			^item aligned
			^item moved-to-color
			^item turned-first-time
			^item turned-180)
}


sp {apply*exit-junction*remove-items
	(state <s>	^operator <o>
				^<item> <v>)
	(<o>	^name exit-junction
			^item <item>)
-->
	(<s>	^<item> <v> -)
}

sp {apply*exit-junction*update-current-direction
	(state <s>	^operator.name exit-junction
				^current-direction <current-direction> <> <selected-direction>
				^selected-direction <selected-direction>)
-->
	(<s>	^current-direction <current-direction> -
			^current-direction <selected-direction>)
}

sp {apply*exit-junction*update-previous-junction-color
	(state <s>	^operator.name exit-junction
				-^using-epmem
				^previous-junction-color <prev-color>
				^junction-color <new-color>)
-->
	(<s>	^previous-junction-color <prev-color> -
			^previous-junction-color <new-color>)
}

sp {apply*exit-junction*update-previous-junction-color*epmem
	(state <s>	^operator.name exit-junction
				^using-epmem
				^previous-junction-color <prev-color>)
-->
	(<s>	^previous-junction-color <prev-color> -)
}
