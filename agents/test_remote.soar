# init-agent - initializes the agent
sp {topstate*propose*init-agent
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator <o> +)
   (<o> ^name init-agent)
}

sp {topstate*apply*init-agent
   (state <s> ^operator.name init-agent)
-->
   (<s> ^topstate <s>
        ^name ev3-agent)
}

sp {topstate*apply*init-agent*change*sensor
	(state <s> ^operator.name init-agent
						 ^io.output-link <out>)
-->
	(<out> ^manager.create-sensor <cs>)
	(<cs> ^port 2
				^type touch)
}

# Will apply the wait operator when there's nothing else to do
sp {topstate*propose*wait
   (state <s> ^superstate <ss>
             -^operator.name wait)
-->
   (<s> ^operator <o> + < =)
   (<o> ^name wait)
}

# Elaborates the remote sensor onto the topstate
sp {topstate*elaborate*remote-sensor
   (state <s> ^name ev3-agent
              ^io.input-link.sensor <remote>)
   (<remote> ^type ir-remote)
-->
   (<s> ^remote-sensor <remote>)
}

sp {topstate*elaborate*color-sensor
   (state <s> ^name ev3-agent
              ^io.input-link.sensor <color>)
   (<color> ^type color)
-->
   (<s> ^color-sensor <color>)
}

# Removes output-link commands when a status is put on them
sp {topstate*remove*completed*actions*d1
   (state <s> ^name ev3-agent
              ^operator <op>
              ^io.output-link <out>)
   (<out> ^<att> <cmd>)
   (<cmd> ^status <status>)
-->
   (<out> ^<att> <cmd> -)
}

# If there was an error, print it
sp {topstate*print*command*error
   (state <s> ^name ev3-agent
              ^io.output-link.<name> <cmd>)
   (<cmd> ^error <error>)
-->
   (write |Error on | <name> | = '| <error> |'| (crlf))
}

# if the remote is not in remote mode, changes the mode
sp {topstate*propose*change-remote-mode
   (state <s> ^name ev3-agent
              ^remote-sensor <remote>)
   (<remote> ^port <port>
             ^mode <> beacon)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name change-remote-mode
        ^port <port>)
}
sp {topstate*prefer*color*over*remote
	(state <s> ^name ev3-agent
						 ^operator <o1> +
						 ^operator <o2> +)
	(<o1> ^name change-color-mode)
	(<o2> ^name change-remote-mode)
-->
	(<s> ^operator <o1> > <o2>)
}

sp {topstate*apply*change-remote-mode
   (state <s> ^name ev3-agent
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name change-remote-mode
        ^port <port>)
-->
   (<out> ^sensor <sensor>)
   (<sensor> ^port <port>
             ^set.mode beacon)
}

sp {topstate*propose*change-color-mode
   (state <s> ^name ev3-agent
              ^color-sensor <color>)
   (<color> ^port <port>
             ^mode <> color)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name change-color-mode
        ^port <port>)
}

sp {topstate*apply*change-color-mode
   (state <s> ^name ev3-agent
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name change-color-mode
        ^port <port>)
-->
   (<out> ^sensor <sensor>)
   (<sensor> ^port <port>
             ^set.mode color)
}

# When a button is pressed, this catches the event
sp {topstate*propose*handle-button-press
   (state <s> ^name ev3-agent
              ^remote-sensor <remote>)
   (<remote> ^mode remote
             ^button <button>)
   (<button> ^prev-state released
             ^cur-state pressed)
-->
   (<s> ^operator <o> + =)
   (<o> ^name handle-button-press
        ^button <button>)
}


# Red-up button is pressed, go forward
sp {topstate*apply*handle-button-press*red-up
   (state <s> ^name ev3-agent
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name handle-button-press
        ^button.name red-up)
-->
   (<out> ^motor <left>
          ^motor <right>)
   (<left> ^port 1
           ^start <left-start>)
   (<left-start> ^direction forward
                 ^power 40)
   (<right> ^port 2
            ^start <right-start>)
   (<right-start> ^direction forward
                  ^power 40)
}

# Red down button is pressed, go backward 
sp {topstate*apply*handle-button-press*red-down
   (state <s> ^name ev3-agent
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name handle-button-press
        ^button.name red-down)
-->
   (<out> ^motor <left>
          ^motor <right>)
   (<left> ^port 1
           ^start <left-start>)
   (<left-start> ^direction backward
                 ^power 40)
   (<right> ^port 2
            ^start <right-start>)
   (<right-start> ^direction backward
                  ^power 40)
}

# blue-up button is pressed, turn left 
sp {topstate*apply*handle-button-press*blue-up
   (state <s> ^name ev3-agent
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name handle-button-press
        ^button.name blue-up)
-->
   (<out> ^motor <left>
          ^motor <right>)
   (<left> ^port 1
           ^start <left-start>)
   (<left-start> ^direction backward
                 ^power 40)
   (<right> ^port 2
            ^start <right-start>)
   (<right-start> ^direction forward
                  ^power 40)
}

# Blue down button is pressed, turn right 
sp {topstate*apply*handle-button-press*blue-down
   (state <s> ^name ev3-agent
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name handle-button-press
        ^button.name blue-down)
-->
   (<out> ^motor <left>
          ^motor <right>)
   (<left> ^port 1
           ^start <left-start>)
   (<left-start> ^direction forward
                 ^power 40)
   (<right> ^port 2
            ^start <right-start>)
   (<right-start> ^direction backward
                  ^power 40)
}


# Button is released, stop
sp {topstate*propose*handle-button-release
   (state <s> ^name ev3-agent
              ^remote-sensor <remote>)
   (<remote> ^button <button>)
   (<button> ^prev-state pressed
             ^cur-state released)
-->
   (<s> ^operator <o> + =)
   (<o> ^name handle-button-release)
}

sp {topstate*apply*handle-button-release*stop
   (state <s> ^name ev3-agent
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name handle-button-release)
-->
   (<out> ^motor <left>
          ^motor <right>)
   (<left> ^port 1
           ^stop <ls>)
   (<right> ^port 2
            ^stop <rs>)
}
   


