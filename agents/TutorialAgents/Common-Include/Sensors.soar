########################################################
#
# Common-Include/Sensors.soar
# Includes:
#   elaborate ^color-sensor-value
#   operator change-color-mode
#   operator create-touch-sensor
#
#########################################################

# Elaborates the color sensor value onto the top state
sp {Common-Include*Sensors*elaborate*color-sensor*left
   (state <s>	^name line-follower
				^color-sensor-values <csv>
				^left-color-sensor-port <lcsp>
				^io.input-link.sensor <color>)
   (<color>	^type color
			^value <color-value> <> none
			^port <lcsp>)
-->
	(<csv>	^left <color-value>)
	(write |  Left Color Sensor = | <color-value> (crlf))
}

sp {Common-Include*Sensors*elaborate*color-sensor*right
	(state <s>	^name line-follower
				^color-sensor-values <csv>
				^right-color-sensor-port <rcsp>
				^io.input-link.sensor <color>)
	(<color>	^type color
				^value <color-value> <> none
				^port <rcsp>)
-->
	(<csv>	^right <color-value>)
	(write |  Right Color Sensor = | <color-value> (crlf))
}

sp {Common-Include*Sensors*elaborate*color-sensor*left*none
   (state <s>	^name line-follower
				^color-sensor-values <csv>
				^left-color-sensor-port <lcsp>
				^io.input-link.sensor <color>)
   (<color>	^type color
			^value none
			^port <lcsp>)
-->
	(<csv>	^left black)
	(write |  Left Color Sensor = | black (crlf))
}

sp {Common-Include*Sensors*elaborate*color-sensor*right*none
	(state <s>	^name line-follower
				^color-sensor-values <csv>
				^right-color-sensor-port <rcsp>
				^io.input-link.sensor <color>)
	(<color>	^type color
				^value none
				^port <rcsp>)
-->
	(<csv>	^right black)
	(write |  Right Color Sensor = | black (crlf))
}

# If this is the follow-line substate, elaborate the
# color sensor value from the top state to this substate
#sp {Common-Include*Sensors*elaborate*color-sensor*substate
#   (state <s>	^name follow-line
#				^superstate.color-sensor-value <value>)
#-->
#   (<s>	^color-sensor-value <value>)
#}

###################################################
#	change-color-mode operator
#
#	If the color sensor is not in "color" mode,
#	this will be proposed and selected.  When it
#	applies it will put on the output link the
#	command to set the mode to "color"
###################################################

# Proposal Rule
sp {Common-Include*Sensors*propose*change-color-mode
   (state <s>	^name line-follower
				^io.input-link.sensor <color>)
   (<color>	^type color
			^port <port>
			^mode <> color)
-->
   (<s>	^operator <o> + > =)	# We should always do this
							# as this is a critical
							# operator to the program
							# working as we intend.
   (<o>	^name change-color-mode
		^port <port>)
}

sp {Common-Include*Sensors*preference*color-mode-better-than-all
	(state <s>	^operator <o> +
				^operator <o2> +)
	(<o>	^name change-color-mode)
	(<o2>	^name <> change-color-mode)
-->
	(<s>	^operator <o> > <o2>)
}

# Apply rule
# Create the command on the output link
sp {Common-Include*Sensors*apply*change-color-mode
   (state <s> ^name line-follower
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name change-color-mode
        ^port <port>)
-->
   (<out> ^sensor <sensor>)
   (<sensor> ^port <port>
             ^set-mode color)
}

###################################################
#	create-touch-sensor operator
#
# Creates a touch sensor on the desired port
# (Touch sensor is not auto-detected)
###################################################

# Proposal Rule
#sp {Common-Include*Sensors*propose*create-touch-sensor
#   (state <s>	^name line-follower
#               ^io.input-link <il>
#               ^touch-sensor-port <port>)
#  (<il> -^sensor.type touch)
#-->
#   (<s>	^operator <o> + > =)	# We should always do this
#							# as this is a critical
#							# operator to the program
#							# working as we intend.
#   (<o> ^name create-touch-sensor
#         ^port <port>)
#}

# Apply rule
# Create the command on the output link
sp {Common-Include*Sensors*apply*create-touch-sensor
   (state <s> ^name line-follower
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name create-touch-sensor
        ^port <port>)
-->
  (<out> ^manager.create-sensor <create>)
  (<create> ^type touch
             ^port <port>)
}
###################################################
#	change-remote-mode operator
#
#	If the remote sensor is not in "beacon" mode,
#	this will be proposed and selected.  When it
#	applies it will put on the output link the
#	command to set the mode to "beacon"
###################################################

# Proposal Rule
sp {Common-Include*Sensors*propose*change-remote-mode
   (state <s>	^name line-follower
				^io.input-link.sensor <remote>)
   (<remote>	^type ir-remote
			^port <port>
			^mode <> beacon)
-->
   (<s>	^operator <o> + > =)	# We should always do this
							# as this is a critical
							# operator to the program
							# working as we intend.
   (<o>	^name change-remote-mode
		^port <port>)
}

# Apply rule
# Create the command on the output link
sp {Common-Include*Sensors*apply*change-remote-mode
   (state <s> ^name line-follower
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name change-remote-mode
        ^port <port>)
-->
   (<out> ^sensor <sensor>)
   (<sensor> ^port <port>
             ^set-mode beacon)
}
