
sp {traverse-junction-simulators*propose*turn
	(state <s>	^name traverse-junction-simulator
				^superstate <ss>
				^relative-direction <> forward)
	(<ss>	^has-centered
			-^turned)
-->
	(<s>	^operator <o>)
	(<o>	^name turn)
}

sp {traverse-junction-simulators*apply*turn*finish*left-right
	(state <s>	^name traverse-junction-simulator
				^superstate <ss>
				^relative-direction << left right >>
				^io.output-link <out>)
	(<ss>	^mark 2
			-^turned)
-->
	(<ss>	^turned true)
	(<out>	^motor <A> <B> <C> <D>)
	(<A>	^port A
			^set-power 0)
	(<B>	^port B
			^set-power 0)
	(<C>	^port C
			^set-power 0)
	(<D>	^port D
			^set-power 0)
}

sp {traverse-junction-simulators*apply*turn*finish*backward
	(state <s>	^name traverse-junction-simulator
				^superstate <ss>
				^relative-direction backward
				^io.output-link <out>)
	(<ss>	^mark 4
			-^turned)
-->
	(<ss>	^turned true)
	(<out>	^motor <A> <B> <C> <D>)
	(<A>	^port A
			^set-power 0)
	(<B>	^port B
			^set-power 0)
	(<C>	^port C
			^set-power 0)
	(<D>	^port D
			^set-power 0)
}

sp {traverse-junction-simulator*apply*turn*mark-1
	(state <s>	^name traverse-junction-simulator
				^operator.name turn
				^relative-direction <> forward
				^superstate <ss>
				^color-sensor-values <csv>)
	(<ss>		^operator.name traverse-junction-simulator
				^has-centered
				^junction-color <color>)
	(<csv>	^left <color>
			^right <color>)
-->
	(<ss>	^mark 1)
}

sp {traverse-junction-simulator*apply*turn*mark-2
	(state <s>	^name traverse-junction-simulator
				^operator.name turn
				^relative-direction <> forward
				^superstate <ss>
				^color-sensor-values <csv>)
	(<ss>		^operator.name traverse-junction-simulator
				^has-centered
				^junction-color <color>
				^mark 1)
	(<csv>	^left black
			^right black)
-->
	(<ss>	^mark 2)
}

sp {traverse-junction-simulator*apply*turn*mark-3
	(state <s>	^name traverse-junction-simulator
				^operator.name turn
				^relative-direction <> forward
				^superstate <ss>
				^color-sensor-values <csv>)
	(<ss>		^operator.name traverse-junction-simulator
				^has-centered
				^junction-color <color>
				^mark 2)
	(<csv>	^left <color>
			^right <color>)
-->
	(<ss>	^mark 3)
}

sp {traverse-junction-simulator*apply*turn*mark-4
	(state <s>	^name traverse-junction-simulator
				^operator.name turn
				^relative-direction <> forward
				^superstate <ss>
				^color-sensor-values <csv>)
	(<ss>		^operator.name traverse-junction-simulator
				^has-centered
				^junction-color <color>
				^mark 3)
	(<csv>	^left black
			^right black)
-->
	(<ss>	^mark 4)
}
