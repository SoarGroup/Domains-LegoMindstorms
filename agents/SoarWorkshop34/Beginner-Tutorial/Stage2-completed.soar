###################################################
# Common Includes (init, wait, etc.)
# These are provided for you.
###################################################

source ../Common-Include/Common-Include_source.soar

###################################################
# This file contains the following operators:
#	follow-line
#	move-left
#	move-right
#	move-forward
#
# This should be a total of nine rules
###################################################

###################################################
#	follow-line operator
###################################################

sp {Stage2*propose*follow-line
	(state <s>	^name line-follower
				^mode follow)
-->
	(<s>	^operator <o> +)
	(<o>	^name follow-line)
}

####################################################
##	follow-line*init-substate operator
####################################################
#
sp {Stage2*elaboration*copy-substate-structures
	(state <s>	^name follow-line
				^superstate <ss>)
	(<ss>	^name line-follower
				^io <io>
				^color-sensor-value <color>
			^left-motor-port <lmp>
			^right-motor-port <rmp>
			^color-left <cl>
			^color-right <cr>
			^color-forward <cf>)
-->
	(<s>	^io <io>
			 ^color-sensor-value <color>
			 ^left-motor-port <lmp>
			^right-motor-port <rmp>
			^color-left <cl>
			^color-right <cr>
			^color-forward <cf>)
}

# There are a couple changes to these operators
# between Stage 1 and Stage 2
#
# 1) The state in which they execute in is the
#    "follow-line" substate instead of the
#    "line-follower" substate
# 2) The operator proposals no longer test for the
#    ^mode follow attribute as the entire substate
#    will only exist if the mode is follow.
# 3) The apply rules grab the io link from the
#    superstate as substates do not contain the io
#    link.

###################################################
#	move-forward operator
###################################################

# Propose the move-forward operator if the current
# detected color sensor value is the forward value
# and our mode is "follow"
sp {Stage2*propose*move-forward
	(state <s>	^name follow-line
				^color-forward <value>
				^color-sensor-value <value>)
-->
	(<s> ^operator <o> +)
	(<o> ^name move-forward)
}

# Apply the move-forward operator if we have the
# move-forward operator selected, an output link,
# a left motor port, and a right motor port.
sp {Stage2*apply*move-forward
	(state <s>	^operator.name move-forward
				^io.output-link <out>
				^left-motor-port <left-port>
				^right-motor-port <right-port>)
-->
	# Create a command for each motor on the output
	# link
	(<out>	^motor <left>
			^motor <right>)
	(<left>	^port <left-port>
					^set-direction backward
					^set-power 40)
	(<right>	^port <right-port>
					 ^set-direction backward
					 ^set-power 40)
}

###################################################
#	move-left operator
###################################################

# This move-left operator is the same as the
# move-forward operator except for three changes.
# 1) The operator has been renamed to move-left
# 2) The color-forward test in the proposal has been
#    changed to color-left
# 3) The direction for the left motor has been changed
#    to forward instead of backward.
sp {Stage2*propose*move-left
	(state <s>	^name follow-line
				^color-left <value>
				^color-sensor-value <value>)
-->
	(<s> ^operator <o> +)
	(<o> ^name move-left)
}

sp {Stage2*apply*move-left
	(state <s>	^operator.name move-left
				^io.output-link <out>
				^left-motor-port <left-port>
				^right-motor-port <right-port>)
-->
	(<out>	^motor <left>
			^motor <right>)
	(<left>	^port <left-port>
					^set-direction forward
					^set-power 40)
	(<right>	^port <right-port>
					 ^set-direction backward
					 ^set-power 40)
}

###################################################
#	move-right operator
###################################################

# This move-right operator is the same as the
# move-forward operator except for three changes.
# 1) The operator has been renamed to move-right
# 2) The color-forward test in the proposal has been
#    changed to color-right
# 3) The direction for the right motor has been changed
#    to forward instead of backward.
sp {Stage2*propose*move-right
	(state <s>	^name follow-line
				^color-right <value>
				^color-sensor-value <value>)
-->
	(<s> ^operator <o> +)
	(<o> ^name move-right)
}

sp {Stage2*apply*move-right
	(state <s>	^operator.name move-right
				^io.output-link <out>
				^left-motor-port <left-port>
				^right-motor-port <right-port>)
-->
	(<out>	^motor <left>
			^motor <right>)
	(<left>	^port <left-port>
					^set-direction backward
					^set-power 40)
	(<right>	^port <right-port>
					 ^set-direction forward
					 ^set-power 40)
}

