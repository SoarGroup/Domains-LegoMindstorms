###################################################
#
# File: Common-Include/Stop-Agent
# Contains:
#   toggle-robot-mode operator
#  
###################################################

###################################################
#	toggle-robot-mode operator
#
#	When a button is pressed on the main brick, this
#	catches the event and will switch the modes.
#
#	Modes:
#		follow
#		wait
###################################################

# Proposal Rule
sp {Common-Include*Stop-Agent*propose*toggle-robot-mode
	(state <s>	^name line-follower
             ^io.input-link.brick.<button-name> <button>
				    ^mode <cur-mode> )
	(<button>	^type button
            ^prev-state released
            ^cur-state pressed)
-->
	(<s>	^operator <o> + = >)
	(<o>	^name toggle-robot-mode
			^button <button>
			^mode <cur-mode>)
}

# Apply Rules

# If we were just following, then the button press is to stop
sp {Common-Include*Stop-Agent*apply*toggle-robot-mode*toggle*mode*wait
	(state <s>	^name line-follower
				^operator <o>
				^mode follow)
	(<o>	^name toggle-robot-mode
			^mode follow)
-->
	(<s>	^mode follow -
			^mode wait
			^stop-agent YES) # Create the flag to stop the agent
}

# If we were just waiting, then the button press is to start following
sp {Common-Include*Stop-Agent*apply*toggle-robot-mode*toggle*mode*explore
	(state <s>	^name line-follower
				^operator <o>
				^mode wait)
	(<o>	^name toggle-robot-mode
			^mode wait)
-->
	(<s>	^mode wait -
			^mode follow)
}

