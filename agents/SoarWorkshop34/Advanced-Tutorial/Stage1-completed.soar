source ../Common-Include/Common-Include_source.soar

source Common-Advanced/Common-Advanced_source.soar


sp {Common-Advanced*apply*Init-Agent*set*current-direction
   (state <s> ^operator.name init-agent)
-->
   (<s> ^current-direction west)
}

sp {Common-Advanced*apply*Init-Agent*create*color-map
   (state <s> ^operator.name init-agent)
-->
   (<s> ^color-map <map>)
   (<map> ^red north
          ^yellow west
          ^green east
          ^blue south)
}

sp {Common-Advanced*propose*select-direction
   (state <s> ^name line-follower
              ^color-type junction
             -^selected-direction
              ^direction-map.direction.name <dir>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name select-direction
        ^direction <dir>)
}

sp {Common-Advanced*apply*select-direction
   (state <s> ^name line-follower
              ^operator <o>)
   (<o> ^name select-direction
        ^direction <dir>)
-->
   (<s> ^selected-direction <dir>)
}

sp {Common-Advanced*prefer*right*color
   (state <s> ^name line-follower
              ^operator <o> +
              ^color-sensor-value <color>
              ^color-map.<color> <dir>)
   (<o> ^name select-direction
        ^direction <dir>)
-->
   (<s> ^operator <o> + >)
}